03 suma srazek vsechny roky = 
-- suma srazek v dane lokalite za vsechny roky

-- varianta 1
VAR varianta_1 = CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy[Rok]))
-- TAKTO RADĚJI NE
-- varianta 2
VAR varianta_2 = CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy))

-- TAKTO NEEEEE
VAR varianta_3 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(
        ALL(data),
    data[Lokalita] = SELECTEDVALUE(data[Lokalita])))

RETURN varianta_1




04 suma srazek rok 2000 = 
-- TAKTO NEEE
VAR varianta_1 = 
CALCULATE(
    SUM(data[Srazky]),
        FILTER(
            ALL(data),
        data[Lokalita] = SELECTEDVALUE(data[Lokalita])
        && data[Rok] = 2000))

VAR varianta_2  =
CALCULATE(
    SUM(data[Srazky]),
        FILTER(
            ALL(dim_datumy),
         dim_datumy[Rok] = 2000))

-- Téměř ideální varianta
VAR varianta_3  =
CALCULATE(
    SUM(data[Srazky]),
        FILTER(
            ALL(dim_datumy[Rok]),
         dim_datumy[Rok] = 2000))

-- Pokud jde, tak takto
VAR varianta_4  =
CALCULATE(
    SUM(data[Srazky]),
       dim_datumy[Rok] = 2000)


RETURN varianta_4




33 rok maximalni srazky v mesici = 

VAR tabulka = 
SUMMARIZE(
    FILTER(
    ALL(data)
    ,data[Lokalita] = SELECTEDVALUE(dim_lokality[Lokalita])
    && data[Mesic] = SELECTEDVALUE(dim_datumy[Mesic]))
    ,data[Rok],
    "Srazky",
    SUM(data[Srazky]))

VAR max_srazky = ROUND(
MAXX(tabulka,[Srazky]),2)

VAR min_srazky = 
ROUND(
MINX(tabulka,[Srazky]),2)

VAR _srazky = ROUND(SUM(data[Srazky]),2)

RETURN
SWITCH(TRUE(),
_srazky = max_srazky,"GOLD",
_srazky = min_srazky,"CYAN")




36 mesic max srazek = 

VAR mesicni_srazky = 

SUMMARIZE(
    FILTER(
    ALL(data),
    data[Lokalita] = SELECTEDVALUE(data[Lokalita])
    && data[Rok] = SELECTEDVALUE(data[Rok])),
    data[Mesic],
    "Mesicni srazky",
    SUM(data[Srazky]))

VAR maximalni_mesicni_srazky = ROUND(MAXX(mesicni_srazky,[Mesicni srazky]),2)

RETURN
IF(ROUND(SUM(data[Srazky]),2) = maximalni_mesicni_srazky,"RED")
